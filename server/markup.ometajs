/* Copyright (c) 2012, Ben Trask
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE AUTHORS ''AS IS'' AND ANY
EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY
DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE. */
function escapeHTML(HTML) {
	return (HTML
		.replace(/&/g, "&amp;")
		.replace(/\"/g, "&quot;") //"
		.replace(/</g, "&lt;")
		.replace(/>/g, "&gt;")
	);
}

ometa markup {
	main = [hash:hash] [content:body end] -> hash+body,
	hash = htmlEnc*:x end -> "<a href=\"/id/"+x.join("")+"\">#"+x.join("")+"</a>\n\n",
	content = markup*:x -> x.join(""),
	markup = blockquote | inlinequote | linequote | tag | anchor | htmlEnc,

	blockquote = br (seq('Quote:') | seq("Reply:")) br br bqcontent:content (bqendquote | bqcite):cite br -> "\n<blockquote>"+content+cite+"</blockquote>",
	bqcontent = beginninglinequote?:blq (~bqendquote ~bqcite markup)*:x -> (blq || "")+x.join(""),
	bqendquote = br br (seq("End quote") | seq("End reply")) -> "\n",
	bqcite = br br (namedAnchor | anchor):anchor bqvia*:via -> "\n\n"+anchor+via.join("")+"\n",
	bqvia = br (namedAnchor | anchor):anchor -> "\nVia: "+anchor,

	inlinequote = br (~'"' ~br htmlEnc)*:prefix '"' iqcontent:content seq('" -- ') (iqcite | iqname):cite br -> "\n"+prefix.join("")+"<blockquote>Quote: "+cite+content+"</blockquote>",
	iqcontent = (~seq('" -- ') ~br markup)*:x -> "\n"+x.join(""),
	iqname = (~seq(" <") ~br ~url htmlEnc)+:x -> x.join("")+"\n",
	iqcite = (namedAnchor | anchor):anchor -> anchor+"\n",

	linequote = br beginninglinequote:quote -> "\n"+quote,
	beginninglinequote = seq("> ") (~br markup)+:content br beginninglinequote?:extra -> "<blockquote>"+content.join("")+"</blockquote>"+(extra || ""), // This trailing `br` does not work when the linequote is at the end of a blockquote. [See: Foreign Corrupt Practices Act.txt]

	anchor = url:url -> "<a href=\""+url+"\">"+decodeURI(url)+"</a>",
	namedAnchor = (~seq(" <") ~br ~url htmlEnc)+:name seq(" <") anchor:anchor '>' -> name.join("")+" &lt;"+anchor+"&gt;",
	url = fullURL,// | bareDomain,
//	bareDomain = (letter | digit | '-')+ '.' seq('com' 'net' 'org' 'edu')
	fullURL = (scheme seq('://') urlChars), // FIXME: http://www.example.com/. includes the trailing period (and comma) in the link.
	scheme = (letter | digit | '-')+,
	urlChars = (letter | digit | '.' | ',' | '/' | '-' | '#' | '_' | '(' | ')' | '?' | '=' | htmlEncAmp | ':' | '~' | '%' | '{' | '}' | '+')+,
	// file:///Users/ben/Desktop/site/dst/sequence-breaking.html

	tag = '#' (letter | digit | '-' | '_')+:x -> "<a href=\"/id/"+x.join("")+"\">#"+x.join("")+"</a>",

	htmlEnc = htmlEncAmp | htmlEncQuot | htmlEncApos | htmlEncLt | htmlEncGt | anything,
	htmlEncAmp = '&' -> "&amp;",
	htmlEncQuot = quote -> "&quot;",
	htmlEncApos = apostrophe -> "&apos;",
	htmlEncLt = '<' -> "&lt;",
	htmlEncGt = '>' -> "&gt;",

//	censored = htmlEnc,
//	phoneNumber = phoneWithCountry | phoneWithArea | phoneBase,
//	phoneBase = <digit digit digit>:three '-' digit digit digit digit -> "xxx-xxxx",
//	phoneWithArea = <digit digit digit>:area '-' phoneBase:num -> area+'-'+num,
//	phoneWithCountry = digit:country '-' phoneWithArea:num -> country+'-'+num,

	quote = '"',
	apostrophe = '\'',
	br = '\n' | '\r'
}
